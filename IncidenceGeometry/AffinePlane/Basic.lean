import IncidenceGeometry.Defs

open IncidenceGeometry

namespace AffinePlane

variable (P L : Type*) [AffinePlane P L]

theorem nondeg : âˆƒ p : Fin 3 â†’ P, Function.Injective p âˆ§ Â¬ Collinear L p :=
  AffinePlane.nondeg'

instance exists_point : Nonempty P := by
  obtain âŸ¨p, _âŸ© := nondeg P L
  use p 0

instance exists_line : Nonempty L := by
  obtain âŸ¨pâŸ© := exists_point P L
  use join p p

variable (P : Type*) {L : Type*} [instPlane : AffinePlane P L]

/-- Every line has a point. -/
theorem exists_point_on_line (l : L) : âˆƒ p : P, p ğˆ l := by
  obtain âŸ¨p, pinj, hpâŸ© := nondeg P L
  by_cases hâ‚€ : p 0 ğˆ l
  Â· use p 0
  let lâ‚€â‚ : L := join (p 0) (p 1)
  let lâ‚€â‚‚ : L := join (p 0) (p 2)
  have hpâ‚€â‚ : p 0 ğˆ lâ‚€â‚ := (join_incident (p 0) (p 1) (Function.Injective.ne pinj (by simp))).1
  have hpâ‚€â‚‚ : p 0 ğˆ lâ‚€â‚‚ := (join_incident (p 0) (p 2) (Function.Injective.ne pinj (by simp))).1
  by_cases hâ‚ : Intersect P l lâ‚€â‚
  Â· obtain âŸ¨q, _, _âŸ© := hâ‚
    use q
  by_cases hâ‚‚ : Intersect P l lâ‚€â‚‚
  Â· obtain âŸ¨q, _, _âŸ© := hâ‚‚
    use q
  absurd hp
  use lâ‚€â‚
  intro i
  fin_cases i
  Â· exact hpâ‚€â‚
  Â· exact (join_incident (p 0) (p 1) (Function.Injective.ne pinj (by simp))).2
  Â· rw [par_unique l (p 0) hâ‚€ lâ‚€â‚ lâ‚€â‚‚ hpâ‚€â‚ hpâ‚€â‚‚ hâ‚ hâ‚‚]
    exact (join_incident (p 0) (p 2) (Function.Injective.ne pinj (by simp))).2

/-- Two lines are parallel if they are equal or do not intersect. -/
def IsParallel (lâ‚ lâ‚‚ : L) : Prop := Intersect P lâ‚ lâ‚‚ â†’ lâ‚ = lâ‚‚

--infix:50 " âˆ¥ " => IsParallel
--can we fix this (using outParam) such that P does not need to be given explicitly?

variable {P L : Type*} [instPlane : AffinePlane P L]

theorem parallel_prop (p : P) (l : L) : IsParallel P (par l p) l := by
  intro h
  by_cases hp : p ğˆ l
  Â· apply par_id
    assumption
  Â· exfalso
    apply par_not_intersect l p hp h

/-- There exists exactly one line through `p` parallel to `l`. -/
theorem parallel_unique {p : P} {l l' : L} (hI : p ğˆ l') (hpar : IsParallel P l l') : par l p = l' := by
  by_cases hp : p ğˆ l
  Â· rw [par_id l p hp]
    apply hpar
    use p
  Â· apply par_unique l p hp (par l p) l' (par_incident l p) hI
    Â· rw [intersect_symm_iff]
      apply par_not_intersect
      exact hp
    Â· intro h
      rw [hpar h] at hp
      exact hp hI

theorem isparallel_symm {lâ‚ lâ‚‚ : L} : IsParallel P lâ‚ lâ‚‚ â†’ IsParallel P lâ‚‚ lâ‚ := by
    intro h âŸ¨p, h'âŸ©
    symm
    apply h
    use p, h'.2, h'.1

theorem isparallel_equivalence : Equivalence (@IsParallel P L _) where
  refl := fun _ _ â†¦ rfl
  symm := isparallel_symm
  trans := by
    intro lâ‚ lâ‚‚ lâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ âŸ¨p, h'âŸ©
    rw [â† parallel_unique h'.1 (isparallel_symm hâ‚â‚‚), parallel_unique h'.2 hâ‚‚â‚ƒ]

variable (P L) in
/-- Two lines are parallel if they are equal or do not intersect. -/
instance parallelSetoid : Setoid L where
  r := IsParallel P
  iseqv := isparallel_equivalence

/-- If two lines are not parallel, they intersect in a unique point. -/
theorem unique_meet_of_not_parallel {lâ‚ lâ‚‚ : L} (h : Â¬ IsParallel P lâ‚ lâ‚‚) : âˆƒ! p : P, p ğˆ lâ‚ âˆ§ p ğˆ lâ‚‚ := by
  unfold IsParallel at h
  push_neg at h
  obtain âŸ¨âŸ¨p, hâ‚, hâ‚‚âŸ©, hneâŸ© := h
  use p, âŸ¨hâ‚, hâ‚‚âŸ©
  intro p' âŸ¨hâ‚', hâ‚‚'âŸ©
  symm
  by_contra hpne
  rw [unique_join p p' lâ‚ hpne hâ‚ hâ‚', unique_join p p' lâ‚‚ hpne hâ‚‚ hâ‚‚'] at hne
  apply hne
  rfl

/-- The (unique) meet of two non-parallel lines. -/
noncomputable def meet (lâ‚ lâ‚‚ : L) (h : Â¬ IsParallel P lâ‚ lâ‚‚) : P :=
  Classical.choose (unique_meet_of_not_parallel h)

theorem meet_incident (lâ‚ lâ‚‚ : L) (h : Â¬ IsParallel P lâ‚ lâ‚‚) : meet lâ‚ lâ‚‚ h ğˆ lâ‚ âˆ§ meet lâ‚ lâ‚‚ h ğˆ lâ‚‚ :=
  (Classical.choose_spec (unique_meet_of_not_parallel h)).left

theorem unique_meet (lâ‚ lâ‚‚ : L) (h : Â¬ IsParallel P lâ‚ lâ‚‚) (p : P) (hp : p ğˆ lâ‚ âˆ§ p ğˆ lâ‚‚) :
    p = meet lâ‚ lâ‚‚ h :=
  (Classical.choose_spec (unique_meet_of_not_parallel h)).right p hp

variable (P L) in
/-- The set of directions, i.e. equivalence classes of the parallel lines. -/
def Direction := Setoid.classes (parallelSetoid P L : Setoid L)

--rather use quotient type?

--todo: use this API more widely in proofs, instead of the implementation of Direction?
variable (P) in
/-- The direction in which the line `l` lies. -/
def direction_of_line (l : L) : Direction P L := âŸ¨{l' | IsParallel P l' l}, Setoid.mem_classes _ _âŸ©

variable (P) in
theorem mem_direction_of_self (l : L) : l âˆˆ (direction_of_line P l).val := Setoid.refl l

theorem direction_eq_class (l : L) {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) (h : l âˆˆ Ï€) : Ï€ = {l' | IsParallel P l' l} := by
  obtain âŸ¨l'', rflâŸ© := hÏ€
  ext l
  constructor
  Â· intro hl
    exact isparallel_equivalence.trans hl (isparallel_equivalence.symm h)
  Â· intro hl
    exact isparallel_equivalence.trans hl h

/-- Two lines are parallel if and only if the directions they belong to are equal. -/
theorem isparallel_iff_eq_directions (lâ‚ lâ‚‚ : L) {Ï€â‚ Ï€â‚‚ : Set L} (hÏ€â‚ : Ï€â‚ âˆˆ Direction P L) (hÏ€â‚‚ : Ï€â‚‚ âˆˆ Direction P L)
      (hâ‚ : lâ‚ âˆˆ Ï€â‚) (hâ‚‚ : lâ‚‚ âˆˆ Ï€â‚‚) :
    IsParallel P lâ‚ lâ‚‚ â†” Ï€â‚ = Ï€â‚‚ := by
  constructor
  Â· intro h
    rw [direction_eq_class lâ‚ hÏ€â‚ hâ‚, direction_eq_class lâ‚‚ hÏ€â‚‚ hâ‚‚]
    ext l
    constructor
    Â· intro hl
      exact isparallel_equivalence.trans hl h
    Â· intro hl
      exact isparallel_equivalence.trans hl (isparallel_equivalence.symm h)
  Â· intro h
    change parallelSetoid P L lâ‚ lâ‚‚
    rw [Setoid.rel_iff_exists_classes]
    use Ï€â‚, hÏ€â‚, hâ‚
    rw [h]
    exact hâ‚‚

--use the two theorems above to golf a few of the proofs below?

/-- Every direction contains a line. -/
theorem line_of_direction {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) : âˆƒ l : L, l âˆˆ Ï€ := by
  have hpart : Setoid.IsPartition (Direction P L) := by
    apply Setoid.isPartition_classes
  have Ï€_nempty : Ï€ â‰  âˆ… := by
    intro h
    rw [h] at hÏ€
    exact hpart.1 hÏ€
  by_contra!
  apply Ï€_nempty
  rw [â† Set.subset_empty_iff]
  intro l hl
  rw [Set.mem_empty_iff_false]
  apply this l hl

/-- If `Ï€` is a direction, then every point lies on exactly one line of `Ï€`.-/
theorem direction_partitions {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) (p : P) : âˆƒ! l : L, p ğˆ l âˆ§ l âˆˆ Ï€ := by
  obtain âŸ¨l', hl'âŸ© := line_of_direction hÏ€
  use par l' p
  simp
  constructor
  Â· use par_incident l' p
    have : parallelSetoid P L (par l' p) l' := parallel_prop p l'
    rw [Setoid.rel_iff_exists_classes] at this
    obtain âŸ¨Ï€', hÏ€'âŸ© := this
    obtain âŸ¨Ï€'', hÏ€''âŸ© := (Setoid.isPartition_classes _).2 l'
    simp at hÏ€''
    have hÏ€Ï€'' : Ï€ = Ï€'' := hÏ€''.2 Ï€ hÏ€ hl'
    have hÏ€'Ï€'' : Ï€' = Ï€'' := hÏ€''.2 Ï€' hÏ€'.1 hÏ€'.2.2
    rw [hÏ€Ï€'', â†hÏ€'Ï€'']
    exact hÏ€'.2.1
  Â· intro l hpl hl
    symm
    apply parallel_unique hpl
    change parallelSetoid P L l' l
    rw [Setoid.rel_iff_exists_classes]
    use Ï€, hÏ€

/-- If `Ï€` is a direction, then the lines of `Ï€` partition the points. -/
theorem direction_partitions' {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) : Setoid.IsPartition {{p : P | p ğˆ l} | l âˆˆ Ï€} := by
  constructor
  Â· intro âŸ¨l, hlâ‚, hlâ‚‚âŸ©
    rw [â†Set.subset_empty_iff] at hlâ‚‚
    obtain âŸ¨p, hpâŸ© := exists_point_on_line P l
    apply hlâ‚‚ at hp
    rw [Set.mem_empty_iff_false] at hp
    assumption
  Â· intro p
    obtain âŸ¨l, âŸ¨hpl, hlâŸ©, hluâŸ© := direction_partitions hÏ€ p
    use {p | p ğˆ l}
    simp at *
    constructor
    Â· exact âŸ¨âŸ¨l, hl, rflâŸ©, hplâŸ©
    Â· intro l' hl' hpl'
      rw [hlu l' hpl' hl']

variable (P L) in
theorem ge_3_directions : âˆƒ Ï€ : Fin 3 â†’ Direction P L, Function.Injective Ï€ := by
  obtain âŸ¨p, hinj, hpâŸ© := nondeg P L
  let l : Fin 3 â†’ L := fun i â†¦ join (p i) (p (i + 1))
  have diff : âˆ€ (i j : Fin 3), j = i âˆ¨ j = i + 1 âˆ¨ j = i + 2 := by decide
  let Ï€ : Fin 3 â†’ Direction P L := fun i â†¦ âŸ¨{ l' | IsParallel P l' (l i) }, âŸ¨l i, rflâŸ©âŸ©
  use Ï€
  intro i j hij
  unfold Ï€ at hij
  rw [Subtype.mk.injEq] at hij
  have hij : IsParallel P (l i) (l j) := by
    change parallelSetoid P L _ _
    rw [Setoid.rel_iff_exists_classes]
    use {l' | IsParallel P l' (l i)}
    constructor
    Â· apply Setoid.mem_classes
    Â· constructor
      Â· apply isparallel_equivalence.refl
      Â· rw [hij]
        apply isparallel_equivalence.refl
  rcases diff i j with rfl | rfl | rfl
  Â· rfl
  Â· absurd hp
    use l i
    intro j
    rcases diff i j with rfl | rfl | rfl
    Â· exact (join_incident (p j) (p (j + 1)) (Function.Injective.ne hinj (by simp))).1
    Â· exact (join_incident (p i) (p (i + 1)) (Function.Injective.ne hinj (by simp))).2
    Â· have : l i = l (i + 1) := by
        apply hij
        use p (i + 1)
        use (join_incident (p i) (p (i + 1)) (Function.Injective.ne hinj (by simp))).2
        use (join_incident (p (i + 1)) (p (i + 1 + 1)) (Function.Injective.ne hinj (by simp))).1
      rw [this]
      have : l (i + 1) = join (p (i + 1)) (p (i + 2)) := by
          unfold l
          simp [add_assoc]
      rw [this]
      exact (join_incident (p (i + 1)) (p (i + 2)) (Function.Injective.ne hinj (by simp))).2
  Â· absurd hp
    use l i
    intro j
    rcases diff i j with rfl | rfl | rfl
    Â· exact (join_incident (p j) (p (j + 1)) (Function.Injective.ne hinj (by simp))).1
    Â· exact (join_incident (p i) (p (i + 1)) (Function.Injective.ne hinj (by simp))).2
    Â· have : l (i + 2) = join (p (i + 2)) (p i) := by
          unfold l
          simp [add_assoc]
      have : l i = l (i + 2) := by
        apply hij
        use p i
        use (join_incident (p i) (p (i + 1)) (Function.Injective.ne hinj (by simp))).1
        rw [this]
        use (join_incident (p (i + 2)) (p i) (Function.Injective.ne hinj (by simp))).2
      rw [this]
      exact (join_incident (p (i + 2)) (p (i + 2 + 1)) (Function.Injective.ne hinj (by simp))).1

variable (P) in
/-- For any two lines, there exists a third line not parallel to either of them. -/
theorem exists_line_not_parallel_to_two_lines (lâ‚ lâ‚‚ : L) : âˆƒ l : L, Â¬ IsParallel P lâ‚ l âˆ§ Â¬ IsParallel P lâ‚‚ l := by
  obtain âŸ¨f, finjâŸ© := ge_3_directions P L
  let Ï€â‚ : Direction P L := âŸ¨{ l' | IsParallel P l' lâ‚ }, Setoid.mem_classes _ _âŸ©
  let Ï€â‚‚ : Direction P L := âŸ¨{ l' | IsParallel P l' lâ‚‚ }, Setoid.mem_classes _ _âŸ©
  have : âˆƒ Ï€ : Direction P L, Ï€ â‰  Ï€â‚ âˆ§ Ï€ â‰  Ï€â‚‚ := by
    by_cases h : Ï€â‚ = f 0
    Â· rw [h]
      by_cases h' : Ï€â‚‚ = f 1
      Â· use f 2
        rw [h']
        exact âŸ¨Function.Injective.ne finj (by decide), Function.Injective.ne finj (by decide)âŸ©
      Â· use f 1, Function.Injective.ne finj (by decide), Ne.symm h'
    Â· by_cases h' : Ï€â‚‚ = f 1
      Â· use f 0
        rw [h']
        exact âŸ¨Ne.symm h, Function.Injective.ne finj (by decide)âŸ©
      Â· by_cases h'' : Ï€â‚ = f 2
        Â· use f 1
          rw [h'']
          exact âŸ¨Function.Injective.ne finj (by decide), Ne.symm h'âŸ©
        Â· by_cases h''' : Ï€â‚‚ = f 0
          Â· use f 2
            rw [h''']
            exact âŸ¨Ne.symm h'', Function.Injective.ne finj (by decide)âŸ©
          Â· use f 0
            exact âŸ¨Ne.symm h, Ne.symm h'''âŸ©
  obtain âŸ¨âŸ¨Ï€, hÏ€âŸ©, hâ‚, hâ‚‚âŸ© := this
  obtain âŸ¨l, hlÏ€âŸ© := line_of_direction hÏ€
  use l
  constructor
  Â· intro h
    apply hâ‚
    unfold Ï€â‚
    simp only [Subtype.mk.injEq]
    rw [â†isparallel_iff_eq_directions l lâ‚ hÏ€ Ï€â‚.prop hlÏ€ (isparallel_equivalence.refl _)]
    exact isparallel_equivalence.symm h
  Â· intro h
    apply hâ‚‚
    unfold Ï€â‚‚
    simp only [Subtype.mk.injEq]
    rw [â†isparallel_iff_eq_directions l lâ‚‚ hÏ€ Ï€â‚‚.prop hlÏ€ (isparallel_equivalence.refl _)]
    exact isparallel_equivalence.symm h

noncomputable def lines_through_a_point_equiv_directions (p : P) : {l : L | p ğˆ l} â‰ƒ Direction P L :=
  Equiv.ofBijective (fun âŸ¨l, _ âŸ© â†¦ direction_of_line P l) <| by
  constructor
  Â· intro âŸ¨lâ‚, hlâ‚âŸ© âŸ¨lâ‚‚, hlâ‚‚âŸ© hlâ‚â‚‚
    simp only [Subtype.mk.injEq] at *
    simp only [Set.mem_setOf_eq] at *
    have hÏ€ : { l' | IsParallel P l' lâ‚ } âˆˆ Direction P L := Setoid.mem_classes _ _
    obtain âŸ¨lâ‚ƒ, âŸ¨hlâ‚ƒ, hlâ‚ƒÏ€âŸ©, lâ‚ƒuniqueâŸ© := direction_partitions (direction_of_line P lâ‚).prop p
    rw [lâ‚ƒunique lâ‚ âŸ¨hlâ‚, isparallel_equivalence.refl _âŸ©]
    rw [lâ‚ƒunique lâ‚‚ âŸ¨hlâ‚‚, by rw [hlâ‚â‚‚]; exact isparallel_equivalence.refl _âŸ©]
  Â· intro âŸ¨Ï€, hÏ€âŸ©
    simp only [Set.coe_setOf, Subtype.exists, exists_prop]
    obtain âŸ¨l, âŸ¨hl, hlÏ€âŸ©, lâ‚ƒuniqueâŸ© := direction_partitions hÏ€ p
    use l, hl
    obtain âŸ¨lâ‚, rflâŸ© := hÏ€
    ext lâ‚‚
    constructor <;> simp at *
    Â· intro hlâ‚‚
      exact isparallel_equivalence.trans hlâ‚‚ hlÏ€
    Â· intro hlâ‚‚
      exact isparallel_equivalence.trans hlâ‚‚ (isparallel_symm hlÏ€)

/-- Given a point `p`, the canonical correspondence between directions and lines through `p`. -/
noncomputable def directions_equiv_lines_through_a_point (p : P) : Direction P L â‰ƒ {l : L | p ğˆ l} :=
  (lines_through_a_point_equiv_directions p).symm

theorem mem_direction_of_self' (p : P) (l : L) (hl : p ğˆ l) : l âˆˆ (lines_through_a_point_equiv_directions p âŸ¨l, hlâŸ©).val := mem_direction_of_self P l

theorem line_of_point_of_direction_mem_direction (p : P) {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) :
    (directions_equiv_lines_through_a_point p âŸ¨Ï€, hÏ€âŸ©).val âˆˆ Ï€ := by
  have hâ‚ : lines_through_a_point_equiv_directions p (directions_equiv_lines_through_a_point p âŸ¨Ï€, hÏ€âŸ©) = âŸ¨Ï€, hÏ€âŸ© := Equiv.apply_symm_apply _ _
  have hâ‚‚ : (âŸ¨Ï€, hÏ€âŸ© : Direction P L).val = Ï€ := rfl
  conv =>
    lhs
    rw [â† hâ‚‚, â†hâ‚]
  apply mem_direction_of_self'

theorem unique_line_of_point_of_direction (p : P) {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) (l : L) (hâ‚ : p ğˆ l) (hâ‚‚ : l âˆˆ Ï€) :
    l = (directions_equiv_lines_through_a_point p âŸ¨Ï€, hÏ€âŸ©).val := by
  have : l = (directions_equiv_lines_through_a_point p (lines_through_a_point_equiv_directions p âŸ¨l, hâ‚âŸ©)).val := by
    unfold directions_equiv_lines_through_a_point
    simp only [Equiv.symm_apply_apply]
  rw [this]
  congr
  unfold lines_through_a_point_equiv_directions
  simp only [Equiv.ofBijective_apply]
  simp only [â† (isparallel_iff_eq_directions (P := P) l l (direction_of_line P l).prop hÏ€ (mem_direction_of_self P l) hâ‚‚).mp (Setoid.refl l),
    Subtype.coe_eta]

--more API for the above definitions?

variable (L) in
/-- Any two points lie on the same number of lines. -/
noncomputable def equiv_lines_through_a_point (pâ‚ pâ‚‚ : P) : {l : L | pâ‚ ğˆ l} â‰ƒ {l : L | pâ‚‚ ğˆ l} :=
  (directions_equiv_lines_through_a_point pâ‚).symm.trans (directions_equiv_lines_through_a_point pâ‚‚)

--auxiliary theorem to define a function Ï€ â†’ {p : P | p ğˆ l}
theorem unique_intersection_of_direction_of_line (l l' : L) {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L)
      (hl : Â¬ l âˆˆ Ï€) (hl' : l' âˆˆ Ï€) :
    âˆƒ! p : P, p ğˆ l âˆ§ p ğˆ l' := by
  apply unique_meet_of_not_parallel
  obtain âŸ¨l'', rflâŸ© := hÏ€
  simp only [Set.mem_setOf_eq] at *
  intro h
  apply hl
  exact isparallel_equivalence.trans h hl'

noncomputable def direction_equiv_points_on_a_line_auxâ‚ (l : L) {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) (h : Â¬ l âˆˆ Ï€) :
    Ï€ â†’ {p : P | p ğˆ l} := by
  intro âŸ¨lâ‚, hlâ‚âŸ©
  have := ExistsUnique.exists (unique_intersection_of_direction_of_line l lâ‚ hÏ€ h hlâ‚)
  exact âŸ¨Classical.choose this, (Classical.choose_spec this).1âŸ©

theorem direction_equiv_points_on_a_line_auxâ‚‚ (l l' : L) {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) (hl : Â¬ l âˆˆ Ï€) (hl' : l' âˆˆ Ï€) :
    (direction_equiv_points_on_a_line_auxâ‚ l hÏ€ hl âŸ¨l', hl'âŸ©).val ğˆ l' := by
  have := ExistsUnique.exists (unique_intersection_of_direction_of_line l l' hÏ€ hl hl')
  exact (Classical.choose_spec this).2

/-- Given a line `l` and a direction `Ï€` not containing `l`, the canonical correspondence between
    lines in `Ï€` and points on `l`. -/
noncomputable def direction_equiv_points_on_a_line (l : L) {Ï€ : Set L} (hÏ€ : Ï€ âˆˆ Direction P L) (h : Â¬ l âˆˆ Ï€) :
    Ï€ â‰ƒ {p : P | p ğˆ l} :=
  Equiv.ofBijective (direction_equiv_points_on_a_line_auxâ‚ l hÏ€ h) <| by
  constructor
  Â· intro âŸ¨lâ‚, hâ‚âŸ© âŸ¨lâ‚‚, hâ‚‚âŸ© hâ‚â‚‚
    have : IsParallel P lâ‚ lâ‚‚ := by
      change parallelSetoid P L lâ‚ lâ‚‚
      rw [Setoid.rel_iff_exists_classes]
      use Ï€, hÏ€
    simp only [Subtype.mk.injEq]
    apply this
    use direction_equiv_points_on_a_line_auxâ‚ l hÏ€ h âŸ¨lâ‚, hâ‚âŸ©
    constructor
    Â· apply direction_equiv_points_on_a_line_auxâ‚‚
    Â· rw [hâ‚â‚‚]
      apply direction_equiv_points_on_a_line_auxâ‚‚
  Â· intro âŸ¨p, hpâŸ©
    obtain âŸ¨l', âŸ¨hpl', hl'Ï€âŸ©, hl'uniqueâŸ© := direction_partitions hÏ€ p
    use âŸ¨l', hl'Ï€âŸ©
    obtain âŸ¨q, âŸ¨hql, hql'âŸ©, hquniqueâŸ© := unique_intersection_of_direction_of_line l l' hÏ€ h hl'Ï€
    have hâ‚ : (direction_equiv_points_on_a_line_auxâ‚ l hÏ€ h âŸ¨l', hl'Ï€âŸ©).val = q := by
      apply hqunique
      exact âŸ¨(direction_equiv_points_on_a_line_auxâ‚ l hÏ€ h âŸ¨l', hl'Ï€âŸ©).prop,
        direction_equiv_points_on_a_line_auxâ‚‚ l l' hÏ€ h hl'Ï€âŸ©
    have hâ‚‚ : p = q := by
      apply hqunique
      exact âŸ¨hp, hpl'âŸ©
    ext
    dsimp
    rw [hâ‚, hâ‚‚]

variable (P) in
/-- Any two lines have the same number of points. -/
theorem equiv_points_on_a_line (lâ‚ lâ‚‚ : L) : Nonempty ({p : P | p ğˆ lâ‚} â‰ƒ {p : P | p ğˆ lâ‚‚}) := by
  obtain âŸ¨l, hâ‚, hâ‚‚âŸ© := exists_line_not_parallel_to_two_lines P lâ‚ lâ‚‚
  have hÏ€ : {l' | IsParallel P l' l} âˆˆ Direction P L := Setoid.mem_classes _ _
  exact âŸ¨(direction_equiv_points_on_a_line lâ‚ hÏ€ hâ‚).symm.trans (direction_equiv_points_on_a_line lâ‚‚ hÏ€ hâ‚‚)âŸ©

/-- Any two directions have the same number of lines. -/
theorem equiv_directions {Ï€â‚ Ï€â‚‚ : Set L} (hÏ€â‚ : Ï€â‚ âˆˆ Direction P L) (hÏ€â‚‚ : Ï€â‚‚ âˆˆ Direction P L) : Nonempty (Ï€â‚ â‰ƒ Ï€â‚‚) := by
  obtain âŸ¨lâ‚, hlâ‚âŸ© := line_of_direction hÏ€â‚
  obtain âŸ¨lâ‚‚, hlâ‚‚âŸ© := line_of_direction hÏ€â‚‚
  obtain âŸ¨l, hâ‚, hâ‚‚âŸ© := exists_line_not_parallel_to_two_lines P lâ‚ lâ‚‚
  have hâ‚ : Â¬ l âˆˆ Ï€â‚ := by
    obtain âŸ¨lâ‚', rflâŸ© := hÏ€â‚
    intro h
    exact hâ‚ (isparallel_equivalence.trans hlâ‚ (isparallel_equivalence.symm h))
  have hâ‚‚ : Â¬ l âˆˆ Ï€â‚‚ := by
    obtain âŸ¨lâ‚‚', rflâŸ© := hÏ€â‚‚
    simp at *
    intro h
    exact hâ‚‚ (isparallel_equivalence.trans hlâ‚‚ (isparallel_equivalence.symm h))
  exact âŸ¨(direction_equiv_points_on_a_line l hÏ€â‚ hâ‚).trans (direction_equiv_points_on_a_line l hÏ€â‚‚ hâ‚‚).symmâŸ©

end AffinePlane
