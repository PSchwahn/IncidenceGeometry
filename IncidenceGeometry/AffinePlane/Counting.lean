import IncidenceGeometry.AffinePlane.Basic

namespace AffinePlane

variable (P L : Type*) [AffinePlane P L]

/-- The order of an affine plane is the number of points on each line. -/
def order : Cardinal := Cardinal.mk {p : P | p ùêà Classical.choice (exists_line P L)}

/-- The order of an affine plane is the number of points on each line. -/
noncomputable def finorder : ‚Ñï := Cardinal.toNat (order P L)

variable {P L : Type*} [AffinePlane P L]

theorem finorder_eq_of_order_eq {n : ‚Ñï} (h : order P L = ‚Üën) : finorder P L = n := by
  simp [finorder, h]

variable (P) in
/-- In an affine plane of order `n`, every line has `n` points. -/
theorem card_points_on_a_line' (l : L) : Cardinal.mk {p : P | p ùêà l} = order P L := by
  apply Cardinal.mk_congr
  apply Nonempty.some
  apply equiv_points_on_a_line

/-- In an affine plane of finite order `n`, every line has `n` points. -/
theorem card_points_on_a_line {n : ‚Ñï} (h : finorder P L = n) (l : L) :
    Nat.card {p : P | p ùêà l} = n := by
  unfold Nat.card
  rw [card_points_on_a_line' P l]
  exact h

variable (P L) in
theorem order_ge_2 (hfin : order P L < Cardinal.aleph0) : finorder P L ‚â• 2 := by
  obtain ‚ü®p, pinj, _‚ü© := nondeg P L
  unfold finorder
  let l : L := join (p 0) (p 1)
  rw [‚Üê card_points_on_a_line' P l] at *
  have lfin := Cardinal.lt_aleph0_iff_finite.mp hfin
  have := join_incident (p 0) (p 1) (Function.Injective.ne pinj (by simp)) (L := L)
  let f : Fin 2 ‚Üí {q : P | q ùêà l} := ![‚ü®p 0, this.1‚ü©, ‚ü®p 1, this.2‚ü©]
  have finj : Function.Injective f := by
    intro i j hij
    unfold f at hij
    fin_cases i <;> fin_cases j
    ¬∑ rfl
    ¬∑ simp only [Set.coe_setOf, Set.mem_setOf_eq, Fin.isValue, Fin.zero_eta, Matrix.cons_val_zero,
      Fin.mk_one, Matrix.cons_val_one, Matrix.cons_val_fin_one, Subtype.mk.injEq] at hij
      absurd pinj hij
      exact zero_ne_one
    ¬∑ simp only [Set.coe_setOf, Set.mem_setOf_eq, Fin.isValue, Fin.zero_eta, Matrix.cons_val_zero,
      Fin.mk_one, Matrix.cons_val_one, Matrix.cons_val_fin_one, Subtype.mk.injEq] at hij
      absurd pinj hij
      exact one_ne_zero
    ¬∑ rfl
  have := Nat.card_le_card_of_injective f finj
  simp only [Nat.card_eq_fintype_card, Fintype.card_fin] at this
  assumption

instance finite_points_on_a_line (hfin : order P L < Cardinal.aleph0) (l : L) : Finite {p : P | p ùêà l} := by
  rw [‚Üê Cardinal.lt_aleph0_iff_finite]
  rwa [card_points_on_a_line' P l]

variable (L) in
/-- In an affine plane of finite order `n`, every point lies on `n + 1` lines. -/
theorem card_lines_through_a_point {n : ‚Ñï} (h : finorder P L = n) (p : P) (hfin : order P L < Cardinal.aleph0) :
    Nat.card {l : L | p ùêà l} = n + 1 := by
  --take a line and a point not on it
  obtain ‚ü®f, hinj, hf‚ü© := nondeg P L
  let p' : P := f 0
  let l : L := join (f 1) (f 2)
  have hpl : ¬¨ p' ùêà l := by
    intro h
    apply hf
    use l
    intro i
    fin_cases i
    ¬∑ exact h
    ¬∑ exact (join_incident (f 1) (f 2) (Function.Injective.ne hinj (by simp))).1
    ¬∑ exact (join_incident (f 1) (f 2) (Function.Injective.ne hinj (by simp))).2
  --l has n points, so we can draw n distinct lines through p
  let j : {q : P | q ùêà l} ‚Üí L := fun ‚ü®q, _‚ü© ‚Ü¶ join p' q
  have pisnt : ‚àÄ q : P, q ùêà l ‚Üí p' ‚â† q := by
    intro q hq hpq
    apply hpl
    rw [hpq]
    assumption
  have jinj : Function.Injective j := by
    intro ‚ü®q‚ÇÅ, hq‚ÇÅ‚ü© ‚ü®q‚ÇÇ, hq‚ÇÇ‚ü© hjq
    --show that j q‚ÇÅ = j q‚ÇÇ = l, thus p ùêà l if q‚ÇÅ ‚â† q‚ÇÇ
    by_contra hq
    simp only [Subtype.mk.injEq] at hq
    have lisq‚ÇÅq‚ÇÇ : l = join q‚ÇÅ q‚ÇÇ := unique_join _ _ _ hq hq‚ÇÅ hq‚ÇÇ
    have j‚ÇÅisq‚ÇÅq‚ÇÇ : j ‚ü®q‚ÇÅ, hq‚ÇÅ‚ü© = join q‚ÇÅ q‚ÇÇ := by
      apply unique_join _ _ _ hq
      ¬∑ exact (join_incident p' q‚ÇÅ (pisnt q‚ÇÅ hq‚ÇÅ)).2
      ¬∑ rw [hjq]
        exact (join_incident p' q‚ÇÇ (pisnt q‚ÇÇ hq‚ÇÇ)).2
    apply hpl
    rw [lisq‚ÇÅq‚ÇÇ, ‚Üê j‚ÇÅisq‚ÇÅq‚ÇÇ]
    exact (join_incident p' q‚ÇÅ (pisnt q‚ÇÅ hq‚ÇÅ)).1
  --every line through p is either one of those lines or the parallel of l through p
  have key : {l' : L | p' ùêà l'} = {par l p'} ‚à™ Set.range j := by
    ext l'
    simp only [Set.mem_setOf_eq, Set.coe_setOf, Set.singleton_union, Set.mem_insert_iff,
      Set.mem_range, Subtype.exists]
    constructor
    ¬∑ intro hpl'
      by_cases hll' : IsParallel P l l'
      ¬∑ left
        symm
        exact parallel_unique hpl' hll'
      ¬∑ right
        obtain ‚ü®q, ‚ü®hql, hql'‚ü©, hqunique‚ü© := unique_intersection_of_not_parallel hll'
        use q, hql
        symm
        exact unique_join _ _ _ (pisnt q hql) hpl' hql'
    ¬∑ intro hl'
      rcases hl' with rfl | ‚ü®q, hql, rfl‚ü©
      ¬∑ exact par_incident l p'
      ¬∑ exact (join_incident p' q (pisnt q hql)).1
  --also this union is disjoint
  have disj : Disjoint {par l p'} (Set.range j) := by
    rw [Set.disjoint_singleton_left]
    intro ‚ü®‚ü®q, hql‚ü©, jqpar‚ü©
    apply hpl
    have : par l p' = l := by
      apply parallel_prop p' l
      use q
      constructor
      ¬∑ rw [‚Üê jqpar]
        exact (join_incident p' q (pisnt q hql)).2
      ¬∑ exact hql
    rw [‚Üê this]
    exact par_incident l p'
  --now juggle cardinals. what is even the best formulation of the theorem?
  have cardj := Cardinal.mk_range_eq_of_injective jinj
  rw [card_points_on_a_line' P l] at cardj
  unfold Nat.card
  rw [‚Üê h, Cardinal.eq.mpr ‚ü®equiv_lines_through_a_point L p p'‚ü©, key, Cardinal.mk_union_of_disjoint disj,
    Cardinal.toNat_add (by simp) ?finite]
  ¬∑ simp only [Cardinal.mk_fintype, Fintype.card_unique, Nat.cast_one, map_one]
    rw [‚ÜêCardinal.toNat_lift, cardj, Cardinal.toNat_lift, add_comm]
    rfl
  ¬∑ rw [‚Üê Cardinal.lift_lt, cardj]
    simp only [Cardinal.lift_aleph0, Cardinal.lift_lt_aleph0]
    exact hfin

instance finite_lines_through_a_point (hfin : order P L < Cardinal.aleph0) (p : P) :
    Finite {l : L | p ùêà l} := by
  have : Nat.card {a : L | p ùêà a} > 0 := by
    rw [card_lines_through_a_point L rfl p hfin]
    simp
  exact (Nat.card_pos_iff.mp this).right

variable (P L : Type*) [AffinePlane P L]

/-- An affine plane of finite order `n` has `n ^ 2` points. -/
theorem card_points {n : ‚Ñï} (h : finorder P L = n) (hfin : order P L < Cardinal.aleph0) :
    Nat.card P = n * n := by
  have nge2 := order_ge_2 P L hfin
  rw [h] at nge2
  obtain ‚ü®p‚ü© := exists_point P L
  let P' := {(q, l) : P √ó L | p ‚â† q ‚àß p ùêà l ‚àß q ùêà l}
  --we count the number of these pairs in two different ways.
  have key‚ÇÅ : Nat.card P' = (n + 1) * (n - 1) := by
    --p lies on n + 1 lines, so there are n + 1 possibilities for l
    --each of these lines has n points, and q can be n - 1 of them
    --the set above is the disjoint union of {(q, l) | p ‚â† q ‚àß q ùêà l} where l runs through {l | p ùêà l}:
    let S : L ‚Üí Set (P √ó L) := fun l ‚Ü¶ {u : P √ó L | ‚àÉ q, u = (q, l) ‚àß p ‚â† q ‚àß q ùêà l}
    let T := (l : {a | p ùêà a}) √ó S l.val
    have hTP' : Nat.card P' = Nat.card T := by
      let f : P' ‚Üí T := fun ‚ü®(q, l), ‚ü®hpq, hpl, hql‚ü©‚ü© ‚Ü¶ ‚ü®‚ü®l, hpl‚ü©, ‚ü®(q, l), ‚ü®q, rfl, hpq, hql‚ü©‚ü©‚ü©
      apply Nat.card_eq_of_bijective f
      rw [Function.bijective_iff_existsUnique]
      intro ‚ü®‚ü®l, hpl‚ü©, ‚ü®u, ‚ü®q, hu, hpq, hql‚ü©‚ü©‚ü©
      subst hu
      use ‚ü®(q, l), ‚ü®hpq, hpl, hql‚ü©‚ü©
      constructor
      ¬∑ rfl
      ¬∑ intro ‚ü®‚ü®q', l'‚ü©, ‚ü®hpq', hpl', hq'l‚ü©‚ü©
        dsimp [f]
        intro hq'
        rw [Sigma.ext_iff] at hq'
        simp_all only [Subtype.mk.injEq]
        simp only [Prod.mk.injEq, and_true]
        obtain ‚ü®hl'l, hq'q‚ü© := hq'
        subst hl'l
        rw [heq_eq_eq, Subtype.mk.injEq, Prod.mk.injEq] at hq'q
        exact hq'q.1
    have hS : ‚àÄ l ‚àà {a | p ùêà a}, Cardinal.mk (S l) = (n - 1 : ‚Ñï) := by
      intro l hpl
      let f : {q : P | p ‚â† q ‚àß q ùêà l} ‚Üí S l := fun ‚ü®q, hpq, hql‚ü© ‚Ü¶ ‚ü®(q, l), ‚ü®q, rfl, hpq, hql‚ü©‚ü©
      have hf : Function.Bijective f := by
        rw [Function.bijective_iff_existsUnique]
        intro ‚ü®u, q, hu, hpq, hql‚ü©
        subst hu
        use ‚ü®q, hpq, hql‚ü©
        constructor
        ¬∑ dsimp
        ¬∑ intro ‚ü®q', hpq', hq'l‚ü©
          dsimp [f]
          intro hq'
          simp only [Subtype.mk.injEq, Prod.mk.injEq, and_true] at hq'
          simp only [hq']
      have he := Equiv.lift_cardinal_eq (Equiv.ofBijective f hf)
      have hS' : Cardinal.mk {q : P | p ‚â† q ‚àß q ùêà l} = (n - 1 : ‚Ñï) := by
        have := finite_points_on_a_line hfin l
        have : Finite {q | p ‚â† q ‚àß q ùêà l} := by
          apply Finite.Set.subset {q | q ùêà l}
          simp only [Set.setOf_subset_setOf, and_imp, imp_self, implies_true]
        have h‚ÇÅ : insert p {q : P | p ‚â† q ‚àß q ùêà l} = {q | q ùêà l} := by
          ext x
          rw [Set.mem_insert_iff]
          constructor
          ¬∑ intro h
            rcases h with rfl | h
            ¬∑ exact hpl
            ¬∑ exact h.2
          ¬∑ intro h
            by_cases hxp : x = p
            ¬∑ exact Or.inl hxp
            ¬∑ exact Or.inr ‚ü®Ne.symm hxp, h‚ü©
        have h‚ÇÇ := Cardinal.mk_insert (a := p) (s := {q : P | p ‚â† q ‚àß q ùêà l})
        simp only [ne_eq, Set.mem_setOf_eq, not_true_eq_false, false_and, not_false_eq_true,
          forall_const, h‚ÇÅ, card_points_on_a_line' P l] at h‚ÇÇ
        apply congrArg Cardinal.toNat at h‚ÇÇ
        rw [Cardinal.toNat_add (by apply Cardinal.lt_aleph0_of_finite) (by simp), map_one] at h‚ÇÇ
        subst h
        unfold finorder
        rw [‚Üê Nat.cast_card]
        rw [h‚ÇÇ, add_tsub_cancel_right]
        rfl
      rw [Cardinal.lift_umax, Cardinal.lift_id' (Cardinal.mk ‚Üë(S l))] at he
      rw [‚Üê he]
      simp only [Cardinal.lift_eq_nat_iff, hS']
    have hT : Cardinal.mk T = Cardinal.sum fun (l : {a | p ùêà a}) ‚Ü¶ Cardinal.mk (S l.val) := by
      apply Cardinal.mk_sigma
    have hp : Cardinal.mk {a : L | p ùêà a} = (n + 1 : ‚Ñï) := by
      have := finite_lines_through_a_point hfin p
      rw [‚Üê Nat.cast_card, card_lines_through_a_point L h p hfin]
    conv at hT =>
      rhs
      congr
      intro l
      rw [hS l.val l.prop]
    rw [Cardinal.sum_const, hp] at hT
    have hT' : Nat.card T = (n + 1) * (n - 1) := by
      unfold Nat.card
      rw [hT]
      simp only [Cardinal.lift_natCast, map_mul, Cardinal.toNat_natCast]
    rw [hTP', hT']
  have key‚ÇÇ : Nat.card P = Nat.card P' + 1 := by
    --there are Nat.card P - 1 possibilities for q, but then l = join p q is unique
    let f : {q : P | p ‚â† q} ‚Üí P' := fun ‚ü®q, hpq‚ü© ‚Ü¶ ‚ü®(q, join p q), ‚ü®hpq, join_incident p q hpq‚ü©‚ü©
    have hf : Function.Bijective f := by
      rw [Function.bijective_iff_existsUnique]
      intro ‚ü®(q, l), ‚ü®hpq, hpl, hql‚ü©‚ü©
      use ‚ü®q, hpq‚ü©
      constructor
      ¬∑ simp only [Subtype.mk.injEq, Prod.mk.injEq, true_and, f]
        rw [unique_join _ _ _ hpq hpl hql]
      ¬∑ intro ‚ü®q', hpq'‚ü©
        simp only [Subtype.mk.injEq, Prod.mk.injEq, and_imp, f]
        intro hq'q
        subst hq'q
        simp only [implies_true]
    have h‚ÇÅ : insert p {q : P | p ‚â† q} = ‚ä§ := by
      ext x
      simp only [Set.mem_insert_iff, Set.mem_setOf_eq, Set.top_eq_univ, Set.mem_univ, iff_true]
      by_cases hxp : x = p
      ¬∑ exact Or.inl hxp
      ¬∑ exact Or.inr (Ne.symm hxp)
    have h‚ÇÇ := Cardinal.mk_insert (a := p) (s := {q : P | p ‚â† q})
    simp only [ne_eq, Set.mem_setOf_eq, not_true_eq_false, not_false_eq_true, forall_const,
        h‚ÇÅ, Set.top_eq_univ, Cardinal.mk_univ] at h‚ÇÇ
    have he := Equiv.lift_cardinal_eq (Equiv.ofBijective f hf)
    rw [Cardinal.lift_umax, Cardinal.lift_id' (Cardinal.mk ‚ÜëP')] at he
    unfold Nat.card
    rw [‚Üê he, h‚ÇÇ, Cardinal.toNat_add ?finite (by simp)]
    simp only [map_one, ne_eq, Cardinal.toNat_lift]
    ¬∑ rw [‚ÜêCardinal.lift_lt, he, Cardinal.lift_aleph0, Cardinal.lt_aleph0_iff_finite]
      have hfin' : Nat.card P' > 0 := by
        rw [key‚ÇÅ]
        simp only [gt_iff_lt, add_pos_iff, zero_lt_one, or_true, mul_pos_iff_of_pos_left,
          tsub_pos_iff_lt]
        linarith
      exact (Nat.card_pos_iff.mp hfin').right
  rw [key‚ÇÇ, key‚ÇÅ, ‚Üê mul_self_tsub_one, Nat.sub_add_cancel]
  rw [one_le_mul_self_iff]
  linarith

instance finite_of_order_lt_aleph0 (hfin : order P L < Cardinal.aleph0) : Finite P := by
  have hP := card_points P L rfl hfin
  have nge2 := order_ge_2 P L hfin
  have cardpos : Nat.card P > 0 := by
    rw [hP]
    apply Nat.zero_lt_of_ne_zero
    rw [mul_self_ne_zero]
    linarith
  exact (Nat.card_pos_iff.mp cardpos).right

variable {P L : Type*} [AffinePlane P L]

/-- In an affine plane of finite order `n`, every direction has `n` lines. -/
theorem card_direction {n : ‚Ñï} (h : finorder P L = n) {œÄ : Set L} (hœÄ : œÄ ‚àà Direction P L) :
    Nat.card œÄ = n := by
  obtain ‚ü®l, hl‚ü© := line_of_direction hœÄ
  obtain ‚ü®l', hl', _‚ü© := exists_line_not_parallel_to_two_lines P l l
  have : l' ‚àâ œÄ := by
    intro h
    apply hl'
    rw [isparallel_iff_eq_directions l l' hœÄ hœÄ hl h]
  unfold Nat.card
  rw [‚Üê h, ‚Üê Cardinal.toNat_lift, Cardinal.lift_mk_eq'.mpr ‚ü®direction_equiv_points_on_a_line l' hœÄ this‚ü©,
    Cardinal.toNat_lift]
  apply card_points_on_a_line
  rfl

instance finite_direction (hfin : order P L < Cardinal.aleph0) {œÄ : Set L} (hœÄ : œÄ ‚àà Direction P L) :
    Finite œÄ := by
  have : Nat.card œÄ > 0 := by
    rw [card_direction rfl hœÄ]
    linarith [order_ge_2 P L hfin]
  exact (Nat.card_pos_iff.mp this).right

/-- An affine plane of finite order `n` has `n + 1` directions. -/
theorem card_directions {n : ‚Ñï} (h : finorder P L = n) (hfin : order P L < Cardinal.aleph0) :
    Nat.card (Direction P L) = n + 1 := by
  obtain ‚ü®p‚ü© := exists_point P L
  unfold Nat.card
  rw [‚Üê card_lines_through_a_point L h p hfin, Cardinal.eq.mpr ‚ü®directions_equiv_lines_through_a_point p‚ü©]
  rfl

instance finite_directions (hfin : order P L < Cardinal.aleph0) : Finite (Direction P L) := by
  have : Nat.card (Direction P L) > 0 := by
    rw [card_directions rfl hfin]
    linarith [order_ge_2 P L hfin]
  exact (Nat.card_pos_iff.mp this).right

variable (P L : Type*) [AffinePlane P L]

/-- An affine plane of finite order `n` has `n ^ 2 + n` lines. -/
theorem card_lines {n : ‚Ñï} (h : finorder P L = n) (hfin : order P L < Cardinal.aleph0) :
    Nat.card L = (n + 1) * n := by
  --there are n + 1 directions, each of which has n lines.
  let L' := (d : Direction P L) √ó d.val
  have key : Nat.card L' = Nat.card L := by
    let f : L' ‚Üí L := fun ‚ü®_, l, _‚ü© ‚Ü¶ l
    apply Nat.card_eq_of_bijective f
    rw [Function.bijective_iff_existsUnique]
    intro l
    let œÄ := {l' | IsParallel P l' l}
    use ‚ü®‚ü®œÄ, by apply Setoid.mem_classes‚ü©, ‚ü®l, Setoid.refl l‚ü©‚ü©
    constructor
    ¬∑ dsimp [f]
    ¬∑ intro ‚ü®‚ü®œÄ', hœÄ'‚ü©, ‚ü®l', hl'‚ü©‚ü©
      dsimp [f]
      intro hl'l
      subst hl'l
      have : œÄ' = œÄ := by
        rw [‚Üê isparallel_iff_eq_directions (œÄ‚ÇÇ := œÄ) l' l' hœÄ'
          (by apply Setoid.mem_classes) hl' (Setoid.refl l')]
        exact Setoid.refl l'
      subst this
      rfl
  have lines_per_dir : ‚àÄ œÄ : Set L, œÄ ‚àà Direction P L ‚Üí Cardinal.mk œÄ = n := by
    intro œÄ hœÄ
    have := finite_direction hfin hœÄ
    rw [‚Üê Nat.cast_card, card_direction h hœÄ]
  have card_dirs : Cardinal.mk (Direction P L) = (n + 1 : ‚Ñï) := by
    have := finite_directions hfin
    rw [‚Üê Nat.cast_card, card_directions h hfin]
  have hL' : Cardinal.mk L' = Cardinal.sum fun (d : Direction P L) ‚Ü¶ Cardinal.mk d.val := by
    apply Cardinal.mk_sigma
  rw [‚Üê key]
  unfold Nat.card
  rw [hL']
  conv =>
    lhs
    rhs
    congr
    intro d
    rw [lines_per_dir d.val d.prop]
  rw [Cardinal.sum_const, card_dirs]
  simp only [Cardinal.lift_id, map_mul, Cardinal.toNat_natCast]

end AffinePlane
